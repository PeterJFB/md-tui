// Characters
norwegian_char = _{ "æ" | "ø" | "å" | "Æ" | "Ø" | "Å" }
char           = _{
    ASCII_ALPHANUMERIC
  | norwegian_char
  | "("
  | ")"
  | "{"
  | "}"
  | "/"
  | ":"
  | ";"
  | "-"
  | "."
  | ","
  | "|"
}
table_char     = _{ ASCII_ALPHANUMERIC | norwegian_char | "(" | ")" | "{" | "}" | "/" | ":" | ";" | "-" | "." | "," }
link_char      = _{ ASCII_ALPHANUMERIC | norwegian_char | "{" | "}" | "/" | ":" | ";" | "-" | "." | "," | "-" }
digit          = _{ '0'..'9' }

// Words
word               = { !forbidden_sentence_prefic ~ NEWLINE? ~ " "* ~ char+ ~ " "? }
italic_word        = { char+ ~ " "? }
bold_word          = { char+ ~ " "? }
strikethrough_word = { char+ ~ " "? }
code_word          = { char+ ~ " "? }
link_word          = { (link_char | " ")+ }
link_data          = { (link_char | " ")+ }

// Lines
code                 =  { "`" ~ code_word+ ~ "`" }
programming_language =  { ASCII_ALPHA+ }
code_line            =  { (char | " ")+ }
normal               = _{ word+ }
markdown_link        =  { " "* ~ "[" ~ link_word+ ~ "]" ~ "(#" ~ link_data+ ~ ")" }
external_link        =  { " "* ~ "[" ~ link_word+ ~ "]" ~ "(" ~ link_data+ ~ ")" }
link                 = _{ (markdown_link | external_link) }
table_word           =  { (table_char | code | link | " ")+ }
italic               = _{ ("*" ~ (italic_word | NEWLINE)+ ~ "*") | ("_" ~ (italic_word | NEWLINE)+ ~ "_") }
bold                 = _{ "**" ~ (bold_word | NEWLINE)+ ~ "**" }
strikethrough        = _{ "~~" ~ (strikethrough_word | NEWLINE)+ ~ "~~" }
o_list_counter       =  { digit+ ~ ". " }

// Prefixes
task_open                 =  { "- [ ] " }
task_complete             =  { "- [x] " | "- [X] " }
task_prefix               = _{ task_open | task_complete }
quote_prefix              =  { ">" }
code_block_prefix         =  { "```" }
table_prefix              =  { "|" }
list_prefix               =  { (" "* ~ "-") | (NUMBER ~ ". ") }
forbidden_sentence_prefic =  {
    NEWLINE ~ (task_prefix | quote_prefix | code_block_prefix | table_prefix | list_prefix)
}

sentence = _{ (code | link | italic | bold | strikethrough | normal)+ }

table_row       = { "|" ~ (table_word | " " | "|")+ }
table_seperator = { ("|"? ~ " "? ~ "-"+ ~ " "? ~ "|")+ }

u_list =  { " "* ~ ("-" | "*") ~ " " ~ (sentence | " ")+ }
o_list =  { o_list_counter ~ (sentence | " ")+ }
h1     = _{ "# " ~ (sentence | " ")+ }
h2     = _{ "## " ~ (sentence | " ")+ }
h3     = _{ "### " ~ (sentence | " ")+ }
h4     = _{ "#### " ~ (sentence | " ")+ }

// Blocks
heading        = { (h1 | h2 | h3 | h4) }
list_container = { (NEWLINE? ~ (u_list | o_list))+ }
paragraph      = { sentence+ }
code_block     = { "```" ~ (programming_language | NEWLINE) ~ (code_line | NEWLINE)+ ~ "```" }
table          = { (NEWLINE? ~ (table_seperator | table_row))+ }
quote          = { ">" ~ (sentence | " ")+ }
task           = { NEWLINE? ~ task_prefix ~ sentence }
block_sep      = { NEWLINE }
horizontal_sep = { "---"+ }

comment = _{ "<!--" ~ (char | " ")+ ~ ">" }

txt = {
    (horizontal_sep | task | comment | table | quote | list_container | code_block | heading | paragraph | block_sep+)+
}
