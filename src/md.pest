// Lines
char                 = _{ ASCII_ALPHANUMERIC | "(" | ")" | "{" | "}" | "/" | ":" | ";" | "-" | "." | "," | "|" }
word                 =  { char+ }
table_char           = _{ ASCII_ALPHANUMERIC | "(" | ")" | "{" | "}" | "/" | ":" | ";" | "-" | "." | "," }
inline_code_word     =  { (char)+ }
code_str             =  { (inline_code_word | " " | "|")+ }
programming_language =  { ASCII_ALPHA+ }
code                 =  { "`" ~ code_str ~ "`" }
normal               = _{ (!(NUMBER ~ ".") ~ (word | " "))+ }
code_line            =  { (char | " ")+ }
link                 =  { "[" ~ (sentence | " ")+ ~ "]" ~ "(" ~ (sentence)+ ~ ")" }
sentence             =  { (normal | code | link)+ }
table_sentence       =  { (table_char | code | link)+ }
digit                =  { '0'..'9' }
table_row            =  { "|" ~ (table_sentence | " " | "|")+ }
table_seperator      =  { ("|"? ~ " "? ~ "-"+ ~ " "? ~ "|")+ }
task_empty           =  { "- [ ] " ~ (sentence | " ")+ }
task_complete        =  { "- [x] " ~ (sentence | " ")+ }
u_list               =  { ("-" | "*") ~ " " ~ (sentence | " ")+ }
o_list               =  { digit+ ~ "." ~ " " ~ (sentence | " ")+ }
h1                   = _{ "# " ~ (sentence | " ")+ }
h2                   = _{ "## " ~ (sentence | " ")+ }
h3                   = _{ "### " ~ (sentence | " ")+ }
h4                   = _{ "#### " ~ (sentence | " ")+ }
v_seperator          = _{ NEWLINE }

// Blocks
heading        = { (h1 | h2 | h3 | h4) }
list_container = { (v_seperator? ~ (u_list | o_list))+ }
paragraph      = { (NEWLINE? ~ sentence)+ }
code_block     = { "```" ~ (programming_language | NEWLINE) ~ (code_line | NEWLINE)+ ~ "```" }
table          = { (v_seperator? ~ (table_seperator | table_row))+ }
quote          = { ">" ~ (sentence | " ")+ }
task           = { v_seperator? ~ (task_empty | task_complete) }
block_sep      = { NEWLINE ~ NEWLINE }

txt = {
    (block_sep | table | quote | task | list_container | code_block | heading | paragraph)+
}
