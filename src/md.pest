// Characters
norwegian_char = _{ "æ" | "ø" | "å" | "Æ" | "Ø" | "Å" }
char           = _{ ASCII_ALPHANUMERIC | norwegian_char | "(" | ")" | "{" | "}" | "/" | ":" | ";" | "-" | "." | "," | "|" }
table_char     = _{ ASCII_ALPHANUMERIC | norwegian_char | "(" | ")" | "{" | "}" | "/" | ":" | ";" | "-" | "." | "," }
link_char      = _{ ASCII_ALPHANUMERIC | norwegian_char | "{" | "}" | "/" | ":" | ";" | "-" | "." | "," }
digit          = _{ '0'..'9' }

// Words
word               =  { " "* ~ char+ ~ (" " | NEWLINE)? }
inline_code_word   =  { " "* ~ char+ ~ " "? }
italic_word        =  { char+ ~ " "? }
bold_word          =  { char+ ~ " "? }
strikethrough_word =  { char+ ~ " "? }
link_word          = _{ (link_char | "#" | "-" | " ")+ }

// Lines
code_str             =  { (inline_code_word | " " | "|")+ }
code                 =  { "`" ~ code_str ~ "`" }
programming_language =  { ASCII_ALPHA+ }
code_line            =  { (char | " ")+ }
normal               = _{ (!(NUMBER ~ ".") ~ (word | " "))+ }
link                 =  { " "* ~ "- [" ~ (link_word | " ")+ ~ "]" ~ "(#" ~ (link_word)+ ~ ")" }
table_word           =  { (table_char | code | link | " ")+ }
italic               = _{ ("*" ~ (italic_word | " " | NEWLINE)+ ~ "*") | ("_" ~ (italic_word | " " | NEWLINE)+ ~ "_") }
bold                 = _{ "**" ~ (bold_word | " " | NEWLINE)+ ~ "**" }
strikethrough        = _{ "~~" ~ (strikethrough_word | " " | NEWLINE)+ ~ "~~" }
o_list_counter       =  { digit+ ~ ". " }

sentence  = { (code | italic | bold | strikethrough | normal)+ }
table_row = { "|" ~ (table_word | " " | "|")+ }

table_seperator = { ("|"? ~ " "? ~ "-"+ ~ " "? ~ "|")+ }
task_empty      = { "- [ ] " ~ (sentence | " ")+ }
task_complete   = { "- [x] " ~ (sentence | " ")+ }

u_list =  { ("-" | "*") ~ " " ~ (sentence | " ")+ }
o_list =  { o_list_counter ~ (sentence | " ")+ }
h1     = _{ "# " ~ (sentence | " ")+ }
h2     = _{ "## " ~ (sentence | " ")+ }
h3     = _{ "### " ~ (sentence | " ")+ }
h4     = _{ "#### " ~ (sentence | " ")+ }

v_seperator = _{ NEWLINE }

// Blocks
heading        = { (h1 | h2 | h3 | h4) }
list_container = { (v_seperator? ~ (u_list | o_list))+ }
paragraph      = { (sentence)+ }
code_block     = { "```" ~ (programming_language | NEWLINE) ~ (code_line | NEWLINE)+ ~ "```" }
table          = { (v_seperator? ~ (table_seperator | table_row))+ }
quote          = { ">" ~ (sentence | " ")+ }
task           = { v_seperator? ~ (task_empty | task_complete) }
block_sep      = { NEWLINE ~ NEWLINE | NEWLINE }

comment = _{ "<!--" ~ (char | " ")+ ~ ">" }

txt = {
    (link | comment | block_sep | table | quote | task | list_container | code_block | heading | paragraph)+
}
