// Characters
alt_char       = _{ (!(NEWLINE | " " | "[" | "]") ~ ANY)+ }
b_char         = _{ (!(NEWLINE | comment | " " | "**") ~ ANY)+ }
c_char         = _{ (!(NEWLINE | " " | "`") ~ ANY)+ }
c_line_char    = _{ (!(NEWLINE | "```") ~ ANY)+ }
comment_char   = _{ (!(NEWLINE | "-->") ~ ANY)+ }
digit          = _{ '0'..'9' }
i_char         = _{ (!(NEWLINE | comment | " " | "_" | "*") ~ ANY)+ }
indent         =  { " "* }
latex_char     = _{ (!(NEWLINE | " " | "$") ~ ANY)+ }
link_char      = _{ (!(NEWLINE | " " | "[" | "]" | "(" | ")") ~ ANY)+ }
p_char         = _{ (!(NEWLINE | comment | code | bold_italic | italic | bold | strikethrough | latex | " ") ~ ANY)+ }
s_char         = _{ (!(NEWLINE | comment | " " | "~~") ~ ANY)+ }
t_char         = _{ (!(NEWLINE | comment | code | bold_italic | italic | bold | strikethrough | latex | " " | "|") ~ ANY)+ }
wiki_link_char = _{ (!(NEWLINE | " " | "|" | "[[" | "]]") ~ ANY)+ }

// Words
word               =  {
    !(forbidden_sentence_prefix | latex | bold_italic | bold | italic | strikethrough | code | link) ~ NEWLINE? ~ " "* ~ p_char+
}
t_word             =  {
    !(forbidden_sentence_prefix | latex | bold_italic | bold | italic | strikethrough | code | link) ~ NEWLINE? ~ " "* ~ t_char+
}
alt_word           = _{ " "* ~ alt_char+ }
bold_italic_word   =  { " "* ~ b_char+ }
bold_word          =  { " "* ~ b_char+ }
code_word          =  { " "* ~ c_char+ }
h_word             =  { p_char+ ~ " "? }
italic_word        =  { " "* ~ i_char+ }
latex_word         =  { latex_char+ ~ " "? }
link_data          =  { link_char+ }
link_word          =  { (link_char | " " | "(" | ")")+ }
strikethrough_word =  { " "* ~ s_char+ }
wiki_link_alone    =  { wiki_link_char+ }
wiki_link_data     =  { wiki_link_char+ }
wiki_link_word     =  { (link_char | " ")+ }

// Prefixes
task_open         =  { "- [ ] " }
task_complete     =  { "- [x] " | "- [X] " }
task_prefix       = _{ task_open | task_complete }
quote_prefix      = _{ ">" }
code_block_prefix =  { "```" }
table_prefix      =  { "|" }
list_prefix       =  { (" "* ~ "-") | (NUMBER+ ~ ". ") }
heading_prefix    =  { "#" }

forbidden_sentence_prefix = {
    NEWLINE ~ " "* ~ (task_prefix | quote_prefix | code_block_prefix | table_prefix | list_prefix | heading_prefix)
}

// Lines
alt_text    = { " "* ~ alt_word+ }
bold        = { NEWLINE? ~ " "* ~ !"\\" ~ "**" ~ (bold_word | (NEWLINE ~ (quote_prefix ~ " ")?))+ ~ "**" }
bold_italic = {
    (NEWLINE? ~ " "* ~ !"\\" ~ "***" ~ (bold_italic_word | (NEWLINE ~ (quote_prefix ~ " ")?))+ ~ "***")
  | ((NEWLINE | " ") ~ " "* ~ !"\\" ~ "_**" ~ (bold_italic_word | (NEWLINE ~ (quote_prefix ~ " ")?))+ ~ "**_")
}

code                 =  { NEWLINE? ~ " "* ~ "`" ~ code_word+ ~ "`" }
code_line            =  { NEWLINE ~ (c_line_char | " ")* }
latex                =  { NEWLINE? ~ " "* ~ !"\\" ~ "$"+ ~ " "? ~ (latex_word | (NEWLINE ~ (quote_prefix ~ " ")?))+ ~ "$"+ }
link                 =  { NEWLINE? ~ " "* ~ (link_line | wiki_link) }
link_line            = _{ "[" ~ link_word+ ~ "]" ~ "(" ~ link_data+ ~ ")" }
normal               = _{ word+ }
o_list_counter       =  { digit+ ~ ". " }
programming_language =  { ASCII_ALPHA+ }
strikethrough        =  { NEWLINE? ~ " "* ~ !"\\" ~ "~~" ~ (strikethrough_word | (NEWLINE ~ (quote_prefix ~ " ")?))+ ~ "~~" }
t_normal             = _{ t_word+ }
wiki_link            = _{ ("[[" ~ wiki_link_alone+ ~ "]]") | ("[[" ~ wiki_link_data+ ~ "|" ~ wiki_link_word+ ~ "]]") }

italic = {
    (NEWLINE? ~ " "* ~ !"\\" ~ "*" ~ (italic_word | (NEWLINE ~ (quote_prefix ~ " ")?))+ ~ "*")
  | ((NEWLINE | " ") ~ " "* ~ !"\\" ~ "_" ~ (italic_word | (NEWLINE ~ (quote_prefix ~ " ")?))+ ~ "_")
}

sentence   = _{ (latex | code | link | bold_italic | italic | bold | strikethrough | normal+)+ }
t_sentence = _{ (!"|" ~ (latex | code | link | italic | bold | strikethrough | t_normal))+ }

table_cell      = { "|" ~ " "* ~ t_sentence+ ~ " "* ~ ("|" ~ " "* ~ NEWLINE)? }
table_seperator = { ("|"? ~ (" " | ":")? ~ "-"+ ~ (" " | ":")? ~ "|") }

u_list = { indent ~ ("-" | "*") ~ " " ~ sentence+ }
o_list = { indent ~ o_list_counter ~ sentence+ }

// Headings
h1 = { "# " ~ (h_word | " ")+ }
h2 = { "## " ~ (h_word | " ")+ }
h3 = { "### " ~ (h_word | " ")+ }
h4 = { "#### " ~ (h_word | " ")+ }
h5 = { "##### " ~ (h_word | " ")+ }
h6 = { "###### " ~ (h_word | " ")+ }

// Quote markings
important = { ^"[!important]" }
note      = { ^"[!note]" }
tip       = { ^"[!tip]" }
warning   = { ^"[!warning]" }
caution   = { ^"[!caution]" }

quote_marking = _{ " "* ~ (important | note | tip | warning | caution) }

// Blocks
heading        = { (h1 | h2 | h3 | h4 | h5 | h6) }
list_container = { (NEWLINE? ~ !comment ~ (u_list | o_list))+ }
paragraph      = { sentence+ }
code_block     = { "```" ~ programming_language? ~ code_line+ ~ "```" }
table          = { (NEWLINE? ~ (table_seperator | table_cell))+ }
quote          = { (NEWLINE? ~ " "* ~ ">" ~ (quote_marking | sentence | " ")*)+ }
task           = { NEWLINE? ~ task_prefix ~ sentence }
block_sep      = { NEWLINE }
horizontal_sep = { NEWLINE? ~ "---" ~ "-"* }
image          = { NEWLINE? ~ "![" ~ alt_text ~ "](" ~ link_data+ ~ ")" }

comment = _{ "<!--" ~ (NEWLINE | comment_char)+ ~ "-->" }

txt = {
    (horizontal_sep | image | task | comment | table | quote | list_container | code_block | heading | paragraph | block_sep | " ")+
}
