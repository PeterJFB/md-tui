// Characters
p_char       = _{ (!(NEWLINE | comment | code | bold_italic | italic | bold | strikethrough | latex | " ") ~ ANY)+ }
t_char       = _{ (!(NEWLINE | comment | code | bold_italic | italic | bold | strikethrough | latex | " " | "|") ~ ANY)+ }
c_char       = _{ (!(NEWLINE | " " | "`") ~ ANY)+ }
i_char       = _{ (!(NEWLINE | comment | " " | "_" | "*") ~ ANY)+ }
b_char       = _{ (!(NEWLINE | comment | " " | "**") ~ ANY)+ }
s_char       = _{ (!(NEWLINE | comment | " " | "~~") ~ ANY)+ }
c_line_char  = _{ (!(NEWLINE | "```") ~ ANY)+ }
comment_char = _{ (!(NEWLINE | "-->") ~ ANY)+ }
link_char    = _{ (!(NEWLINE | " " | "[" | "]" | "(" | ")") ~ ANY)+ }
latex_char   = _{ (!(NEWLINE | " " | "$") ~ ANY)+ }
digit        = _{ '0'..'9' }
indent       =  { " "* }

// Words
word               = {
    !(forbidden_sentence_prefix | latex | bold_italic | bold | italic | strikethrough | code | link) ~ NEWLINE? ~ " "* ~ p_char+
}
t_word             = {
    !(forbidden_sentence_prefix | latex | bold_italic | bold | italic | strikethrough | code | link) ~ NEWLINE? ~ " "* ~ t_char+
}
h_word             = { p_char+ ~ " "? }
italic_word        = { i_char+ ~ " "? }
bold_word          = { b_char+ ~ " "? }
bold_italic_word   = { b_char+ ~ " "? }
strikethrough_word = { s_char+ ~ " "? }
code_word          = { c_char+ ~ " "? }
latex_word         = { latex_char+ ~ " "? }
link_word          = { (link_char | " " | "(" | ")")+ }
link_data          = { link_char+ }

// Prefixes
task_open         =  { "- [ ] " }
task_complete     =  { "- [x] " | "- [X] " }
task_prefix       = _{ task_open | task_complete }
quote_prefix      =  { ">" }
code_block_prefix =  { "```" }
table_prefix      =  { "|" }
list_prefix       =  { (" "* ~ "-") | (NUMBER+ ~ ". ") }
heading_prefix    =  { "#" }

forbidden_sentence_prefix = {
    NEWLINE ~ (task_prefix | quote_prefix | code_block_prefix | table_prefix | list_prefix | heading_prefix)
}

// Lines
code                 =  { NEWLINE? ~ " "* ~ "`" ~ code_word+ ~ "`" }
programming_language =  { ASCII_ALPHA+ }
code_line            =  { NEWLINE ~ (c_line_char | " ")* }
normal               = _{ word+ }
t_normal             = _{ t_word+ }
markdown_link        = _{ "[" ~ link_word+ ~ "]" ~ "(#" ~ link_data+ ~ ")" }
external_link        = _{ "[" ~ link_word+ ~ "]" ~ "(" ~ link_data+ ~ ")" }
link                 =  { NEWLINE? ~ " "* ~ (markdown_link | external_link) }
italic               =  {
    (NEWLINE? ~ " "* ~ !"\\" ~ "*" ~ (italic_word | NEWLINE)+ ~ "*")
  | ((NEWLINE | " ") ~ " "* ~ !"\\" ~ "_" ~ (italic_word | NEWLINE)+ ~ "_")
}
bold                 =  { NEWLINE? ~ " "* ~ !"\\" ~ "**" ~ (bold_word | NEWLINE)+ ~ "**" }
bold_italic          =  {
    (NEWLINE? ~ " "* ~ !"\\" ~ "***" ~ (bold_italic_word | NEWLINE)+ ~ "***")
  | ((NEWLINE | " ") ~ " "* ~ !"\\" ~ "_**" ~ (bold_italic_word | NEWLINE)+ ~ "**_")
}
strikethrough        =  { NEWLINE? ~ " "* ~ !"\\" ~ "~~" ~ (strikethrough_word | NEWLINE)+ ~ "~~" }
latex                =  { NEWLINE? ~ " "* ~ !"\\" ~ "$"+ ~ " "? ~ (latex_word | NEWLINE)+ ~ "$"+ }
o_list_counter       =  { digit+ ~ ". " }

sentence   = _{ (latex | code | link | bold_italic | italic | bold | strikethrough | normal+)+ }
t_sentence = _{ (!"|" ~ (latex | code | link | italic | bold | strikethrough | t_normal))+ }

table_cell      = { "|" ~ " "* ~ t_sentence+ ~ " "* ~ ("|" ~ " "* ~ NEWLINE)? }
table_seperator = { ("|"? ~ (" " | ":")? ~ "-"+ ~ (" " | ":")? ~ "|") }

u_list = { indent ~ ("-" | "*") ~ " " ~ sentence+ }
o_list = { indent ~ o_list_counter ~ sentence+ }

// Headings
h1 = _{ "# " ~ (h_word | " ")+ }
h2 = _{ "## " ~ (h_word | " ")+ }
h3 = _{ "### " ~ (h_word | " ")+ }
h4 = _{ "#### " ~ (h_word | " ")+ }
h5 = _{ "##### " ~ (h_word | " ")+ }
h6 = _{ "###### " ~ (h_word | " ")+ }

// Blocks
heading        = { (h1 | h2 | h3 | h4 | h5 | h6) }
list_container = { (NEWLINE? ~ !comment ~ (u_list | o_list))+ }
paragraph      = { sentence+ }
code_block     = { "```" ~ programming_language? ~ code_line+ ~ "```" }
table          = { (NEWLINE? ~ (table_seperator | table_cell))+ }
quote          = { (NEWLINE? ~ ">" ~ (sentence | " ")+)+ }
task           = { NEWLINE? ~ task_prefix ~ sentence }
block_sep      = { NEWLINE }
horizontal_sep = { NEWLINE? ~ "---" ~ "-"* }

comment = _{ "<!--" ~ (comment_char)+ ~ "-->" }

txt = {
    (horizontal_sep | task | comment | table | quote | list_container | code_block | heading | paragraph | block_sep+ | " ")+
}
